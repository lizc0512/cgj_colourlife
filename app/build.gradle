apply plugin: 'com.android.application'
apply plugin:'newlens'
android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.tg.coloursteward"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode 59
        versionName "5.3.2"
        // Enabling multidex support.
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi", "armeabi-v7a", 'arm64-v8a', 'x86', 'x86_64', 'mips', 'mips64'
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "a923c16ad07f805480d67b2b", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('cgj.keystore')
            storePassword '123456'
            keyAlias '彩管家'
            keyPassword '123456'
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    //指定打包后应用名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = "彩管家_v${defaultConfig.versionName}_${getPackageInfo(variant)}_${releaseTime()}.apk"
            } else {
                outputFileName = "彩管家4.0_${defaultConfig.versionName}_${getPackageInfo(variant)}_${releaseTime()}_debug.apk"
            }

            variant.assemble.doLast {
                copyBuildApk()
            }

        }
    }

}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        url "http://maven.aliyun.com/nexus/content/repositories/releases"
    }
    google()
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation project(':huxin_sdk')

    implementation(name: 'gt3geetest_sdk', ext: 'aar')
    implementation 'com.networkbench.newlens.agent.android:nbs.newlens.agent:2.10.1'
    implementation 'org.litepal.android:core:1.3.0'
    implementation 'com.githang:status-bar-compat:0.7'

    implementation 'cn.jiguang.sdk:jpush:3.1.3'  // JPush 3.1.1 版本。
    implementation 'cn.jiguang.sdk:jcore:1.2.1'  // JCore 1.1.9

    implementation 'com.aliyun.ams:alicloud-android-hotfix:3.2.4'
    implementation 'com.loopj.android:android-async-http:1.4.6'
    implementation 'com.github.ittianyu:BottomNavigationViewEx:1.2.4'
    implementation 'com.youth.banner:banner:1.4.9'    //banner公告
    implementation 'com.jcodecraeer:xrecyclerview:1.5.9'
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:support-v4:${SUPPORT_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:design:${SUPPORT_VERSION}"
    implementation "com.android.support:support-v13:${SUPPORT_VERSION}"
    implementation "com.android.support:exifinterface:${SUPPORT_VERSION}"

    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
}

project.afterEvaluate {
    handleBuildTag()
}

def copyBuildApk() {
    copy {
        from("${buildDir}/outputs/apk/release") {
            include "*.apk"
        }
        into '../dist'
    }
}


def handleBuildTag() {
    def launch = getLaunchMode()
    if (launch != null) {
        def appCfgFilePath = "../huxin_sdk/src/main/java/com/youmai/hxsdk/config/AppConfig.java"
        def appCfgRegexStr = "public\\s+final\\s+static\\s+int\\s+LAUNCH_MODE\\s+=\\s+[012];"
        def appCfgReplaceStr = "public final static int LAUNCH_MODE = " + launch + ";"
        handleReplaceSrc project, appCfgRegexStr, appCfgReplaceStr, appCfgFilePath
    }
}


def getLaunchMode() {
    if (project.hasProperty('launch_mode')) {
        return project.getProperties().getAt('launch_mode')
    } else {
        return 1      //default 正式环境
    }
}

def getPackageInfo(variant) {
    def launch = getLaunchMode()

    if (launch == "0") {
        return "test_youmai"
    } else if (launch == "1") {
        return "test_color"
    } else if (launch == "2") {
        return "rel"
    }
}


def static handleReplaceSrc(project, regexStr, replaceStr, filePath) {
    def srcFile = project.file filePath
    def newReplaceStr = srcFile.text.replaceAll regexStr, replaceStr
    srcFile.write newReplaceStr, "UTF-8"
}

def static releaseTime() {
    return new Date().format("MMdd_HHmm")
}
